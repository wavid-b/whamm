// ==============================
// ---- High-Level Structure ----
// ==============================

// supports top-level global declarations/initial assignments and probe definitions
script = { SOI ~ (statement | fn_def)* ~ probe_def ~ (statement | fn_def | probe_def)* ~ EOI }
 
// TODO -- support comma separated list of specs: https://docs.oracle.com/cd/E23824_01/html/E22973/glghi.html#scrolltoc
probe_def = { PROBE_SPEC ~ PUSH(predicate?) ~ "{" ~ statement* ~ "}" }

predicate = { "/" ~ expr ~ "/" }

//making the decision not to support "void" functions -- can change later
fn_def = { ID ~ "(" ~ (( param ) ~ ("," ~ param )*) ? ~ ")" ~ ("->" ~ TYPE) ? ~ block }
param = { TYPE ~ ID }
// =====================
// ---- Identifiers ----
// =====================

PROBE_ID = @{
    (ASCII_ALPHA
    | "_"
    | ASCII_DIGIT
    | "*"
    | "+"
    | "\\"
    | "?"
    | "|"
    | "!"
    | "["
    | "]")+
}

// TODO -- I think we only want the one that has all three colons, or a single PROBE_ID
PROBE_SPEC = ${
    PROBE_ID? ~ ":" ~ PROBE_ID? ~ ":" ~ PROBE_ID? ~ ":" ~ PROBE_ID?
    | PROBE_ID? ~ ":" ~ PROBE_ID? ~ ":" ~ PROBE_ID?
    | PROBE_ID? ~ ":" ~ PROBE_ID?
    | PROBE_ID
}
//disallowed IDs should have the full list of reserved names like return

DISALLOWED_ID = _{ "return" | "if" | "while" | "else" }
ID = @{ (!DISALLOWED_ID ~ (ASCII_ALPHA | "_")+ ~ ( ASCII_DIGIT | (ASCII_ALPHA | "_")+ )*) |
        (DISALLOWED_ID ~ (ASCII_ALPHA | "_" | ASCII_DIGIT)+)
}
// ===============
// ---- Types ----
// ===============

TY_I32 = @{ "i32" }
TY_BOOL = @{ "bool" }
TY_STRING = @{ "str" }

// a tuple that's used as a type declaration
TY_TUPLE = { "(" ~ ")"| "(" ~ TYPE ~ ( "," ~ TYPE )* ~ ")" }
// first TYPE is the type of the key, second TYPE is the type of the value
TY_MAP = { "map<" ~ TYPE ~ "," ~ TYPE ~ ">" }

TYPE = _{ TY_I32 | TY_BOOL | TY_STRING | TY_TUPLE | TY_MAP }

// ====================
// ---- Statements ----
// ====================

arg = { tuple | expr | val | ternary }
fn_call = { ID ~ "(" ~ ( arg )? ~ ( "," ~ arg )* ~ ")" }
block = { "{" ~ statement* ~ "}" }


// var ops
ret = { "return" ~ expr ? }
declaration = { TYPE ~ ID }
assignment = { ID ~ "=" ~ (ternary | expr) }
statement = { ( ( fn_call | declaration | assignment | incrementor | decrementor | ret ) ~ ";" )+ }
incrementor = { ID ~ "++"}
decrementor = { ID ~ "--"}
// =====================
// ---- Expressions ----
// =====================

ternary = { expr ~ "?" ~ expr ~ ":" ~ expr }

expr = { prefix? ~ operand ~ (BINOP ~ prefix? ~ operand)* }

prefix = _{ UNOP }
UNOP = _{ neg }
    neg = { "!" }

val = _{ BOOL | ID | I32 | STRING | tuple}
operand = _{ fn_call | "(" ~ expr ~ ")" | val }

tuple = { "(" ~ ")" | "(" ~ (val) ~ ( "," ~ val )* ~ ")" }

BINOP = _{ LOGOP | RELOP | SUMOP | MULOP }
    // Logical operators
    and = { "&&" }
    or = { "||" }
    LOGOP = _{ and | or }

    // Relational operators
    eq = { "==" }
    ne = { "!=" }
    ge = { ">=" }
    gt = { ">" }
    le = { "<=" }
    lt = { "<" }
    RELOP = _{ eq | ne | ge | gt | le | lt }

    // Highest precedence arithmetic operators
    add = { "+" }
    subtract = { "-" }
    SUMOP = _{ add | subtract }

    // Next highest precedence arithmetic operators
    multiply = { "*" }
    divide = { "/" }
    modulo = { "%" }
    MULOP = _{ multiply | divide | modulo }

// ===================
// ---- Terminals ----
// ===================

// TODO -- make sure that the binary/octal/hexadecimal formats are parsed correctly
// TODO -- add support for negative numbers (see calc-pest tutorial)
I32 = @{
    "0x" ~ ASCII_HEX_DIGIT+ // Hexadecimal digit
    | "0b" ~ ASCII_BIN_DIGIT+ // Binary digit
    | "0" ~ ASCII_OCT_DIGIT+ // Octal digit
    | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ // Multiple digit, base 10
    | ASCII_DIGIT // Single digit, base 10 (account for having just '0')
}

BOOL = @{
    "true"
    | "false"
}

STRING = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

// ===================
// ---- Skip-able ----
// ===================

newline = _{ "\n" | "\r\n" }
WHITESPACE = _{ " " | "\t" | newline }

block_comment = _{ "/*" ~ (block_comment | !"*/" ~ ANY)* ~ "*/" }
COMMENT    = _{ block_comment | ("//" ~ (!newline ~ ANY)*) }
